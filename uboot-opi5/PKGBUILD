# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-opi5
pkgver=2017.09
pkgrel=2
pkgdesc="U-Boot for Orange Pi 5"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
#provides=('uboot-tools')
#conflicts=('uboot-tools')
makedepends=('git' 'bc' 'python' 'swig' 'dtc')
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_uboot="07b1cfbf40b842e0e59b2db39308035b452a6e52"
_commit_rkbin="d6ccfe401ca84a98ca3b85c12b9554a1a43a166c"

source=("$pkgname::git+https://github.com/Danct12/u-boot.git#commit=$_commit_uboot"
        "rkbin-rk::git+https://gitlab.com/rk3588_linux/rk/rkbin#commit=$_commit_rkbin"
        'config'
        'boot.txt'
        'mkscr'
        'update-u-boot'
        'python3.patch')
md5sums=('6a052d8b2890cee29179120df7b5f2eb'
         '77207998378fcbb201ba86e5e1630d02'
         '97539a1c13e14009557e44c96a672a5d'
         '15015d1d8f8739417c04eada40a51491'
         '021623a04afd29ac3f368977140cfbfd'
         'ec7c2005cb5cc9a23d1eb0e1bf089bf9'
         '421a73479cb4571e17d9c99b302f956b')

prepare() {
  cd ${pkgname}
  cp ${srcdir}/config .config

  patch -p1 < ../python3.patch
}

build() {
  cd ${pkgname}

  make EXTRAVERSION=-${pkgrel} \
    BL31=../rkbin-rk/bin/rk35/rk3588_bl31_v1.38.elf spl/u-boot-spl.bin u-boot.dtb u-boot.itb

  # uboot-tools
  #make tools-all
}

package() {
  cd ${pkgname}

  mkdir -p "${pkgdir}/boot"

  ./tools/mkimage -n rk3588 -T rksd -d \
    ${srcdir}/rkbin-rk/bin/rk35/rk3588_ddr_lp4_2112MHz_lp5_2736MHz_v1.11.bin:spl/u-boot-spl.bin \
    idbloader.img

  cp ./idbloader.img "${pkgdir}/boot/idbloader.img"
  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin

  # uboot-tools
  #install -m 755 -d "$pkgdir"/usr/bin
  #install -m 755 -t "$pkgdir"/usr/bin/ tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  #ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  #install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ doc/mkimage.1
}
